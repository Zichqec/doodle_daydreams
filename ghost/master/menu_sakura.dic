OnMouseDoubleClick
{
	if reference5 == 0 //left click
	{
		OnSakuraMenu
	}
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	if SHIORI3FW.Eventid != "OnMouseDoubleClick"; "\C\![lock,balloonrepaint]\c"
	"\0\s[%(CurrentPose)]\![set,autoscroll,disable]\_q"
	if SHIORI3FW.BalloonName == "Doodles in the Margins"; "\b[4]"
	else; "\b[2]"
	
	"\![*]\q[Daydream,OnAiTalk]  "
	switch LastTalk == "" //Uses top option if there is a dialogue to repeat
	{
		"\![*]\__q[OnLastTalk]Remember\__q"
		"\![*]\f[color,disable]Remember\f[color,default]"
	}
	
	"\n\n"
	
	"\![*]\__q[OnPoseMenu]Reposition\__q"
	"\n\n"
	"Random:\n"
	"\![!]\__q[OnRandomChange,pose]Pose\__q  "
	"\![!]\__q[OnRandomChange,clothes]Clothes\__q  "
	"\![!]\__q[OnRandomChange,all]All\__q"
	
	"\n\n"
	
	if SHIORI3FW.BalloonName == "Doodles in the Margins"
	{
		"\![*]\__q[OnBalloonMenu]Paper color\__q"
		"\n\n"
	}
	
	"Talk rate:\n"
	_talktimes = ("Off,0","1m,60","3m,180","5m,300","10m,600","15m,900")
	foreach _talktimes; _time
	{
		_sec = TOINT(_time[1])
		_txt = _time[0]
		switch _sec == aitalkinterval
		{
			"\__q[OnTalkRateChange,%(_sec)]%(_txt)\__q"
			"\f[underline,true]\_a[OnTalkRateChange,%(_sec)]%(_txt)\_a\f[underline,default]"
		}
		"  "
	}
	"\n\n"
	
	"\![*]\q[Cancel,blank]"
	"\![unlock,balloonrepaint]"
}

OnLastTalk
{
	LastTalk
}

OnPoseMenu : all
{
	"\C\![lock,balloonrepaint]\c"
	"\0\s[%(CurrentPose)]\![set,autoscroll,disable]\_q"
	if SHIORI3FW.BalloonName == "Doodles in the Margins"; "\b[4]"
	else; "\b[2]"
	
	"\f[align,center]%(emdash) Pose %(emdash)\n[0]\f[align,default]"
	
	"\n"
	
	"\![*]"
	switch CurrentPose == 0
	{
		"\__q[OnChangePose,0]Adventurer\__q"
		"\_a[OnChangePose,0]Adventurer\_a"
	}
	"\n"
	
	"\![*]"
	switch CurrentPose == 1
	{
		"\__q[OnChangePose,1]Demure\__q"
		"\_a[OnChangePose,1]Demure\_a"
	}
	"\n"
	
	"\![*]"
	switch CurrentPose == 2
	{
		"\__q[OnChangePose,2]Sleepy\__q"
		"\_a[OnChangePose,2]Sleepy\_a"
	}
	"\n"
	"\n"
	
	"\![*]\__q[OnSakuraMenu]Back\__q\n[0]\f[align,right]\__q[blank]Close\__q"
	"\![unlock,balloonrepaint]"
}

OnChangePose
{
	CurrentPose = TOINT(reference0)
	LastTalk = ""
	OnPoseMenu
}

OnRandomChange : all
{
	if reference0 == "pose" || reference0 == "all"
	{
		_num = CurrentPose
		while _num == CurrentPose
		{
			_num = RAND(3) //3 poses, 0-2
		}
		CurrentPose = _num
		LastTalk = ""
	}
	if reference0 == "clothes" || reference0 == "all"
	{
		_currenttop = ""
		_currentbottom = ""
		_currenthat = ""
		_currenthandheld = ""
		_currentextra = ""
		
		//This assumes we will never use the multiple option, as a note. May need to adjust
		foreach Dressups.Current; _dressup
		{
			if _dressup[0] == "Top"; _currenttop = _dressup[1]
			if _dressup[0] == "Bottom"; _currentbottom = _dressup[1]
			if _dressup[0] == "Hat"; _currenthat = _dressup[1]
			if _dressup[0] == "Handheld"; _currenthandheld = _dressup[1]
			if _dressup[0] == "Extra"; _currentextra = _dressup[1]
		}
		
		_newtop = ANY(Dressups.Top)
		_newbottom = ANY(Dressups.Bottom)
		_newhat = ANY(Dressups.Hat)
		_newhandheld = ANY(Dressups.Handheld)
		_newextra = ANY(Dressups.Extra)
		
		//Remove old binds
		"\![bind,Top,,0]"
		"\![bind,Bottom,,0]"
		"\![bind,Hat,,0]"
		"\![bind,Handheld,,0]"
		"\![bind,Extra,,0]"
		
		//Add new ones
		"\![bind,Top,%(_newtop),1]"
		"\![bind,Bottom,%(_newbottom),1]"
		
		//25% chance to skip each of these
		_skipchance = 25
		if RAND(100) >= _skipchance; "\![bind,Hat,%(_newhat),1]"
		if RAND(100) >= _skipchance; "\![bind,Handheld,%(_newhandheld),1]"
		if RAND(100) >= _skipchance; "\![bind,Extra,%(_newextra),1]"
	}
	OnSakuraMenu
}

OnBalloonMenu : all
{
	"\C\![lock,balloonrepaint]\c"
	"\0\s[%(CurrentPose)]\![set,autoscroll,disable]\_q"
	if SHIORI3FW.BalloonName == "Doodles in the Margins"; "\b[4]"
	else; "\b[2]"
	
	"\f[align,center]%(emdash) Paper color %(emdash)\n[0]\f[align,default]"
	"\n"
	
	_colors = ("Red,3","Orange,4","Yellow,5","Green,6","Cyan,7","Blue,0","Purple,1","Pink,2")
	
	foreach _colors; _color
	{
		"\![*]"
		switch TOINT(_color[1]) == BalloonColor
		{
			"\__q[OnChangeBalloonColor,%(_color[1])]%(_color[0])\__q"
			"\_a[OnChangeBalloonColor,%(_color[1])]%(_color[0])\_a"
		}
		"\n"
	}
	"\n"
	
	"\f[align,center]\![*]\__q[OnChangeBalloonColor,random]Random\__q\n\f[align,default]\n"
	
	"\![*]\__q[OnSakuraMenu]Back\__q\n[0]\f[align,right]\__q[blank]Close\__q"
	"\![unlock,balloonrepaint]"
}

OnChangeBalloonColor
{
	if reference0 == "random"
	{
		_num = BalloonColor
		while _num == BalloonColor
		{
			_num = RAND(8) //8 colors, 0-7
		}
		BalloonColor = _num
	}
	else
	{
		BalloonColor = TOINT(reference0)
	}
	OnBalloonMenu
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnSakuraMenu
}