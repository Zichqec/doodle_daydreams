OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	if SHIORI3FW.BalloonName == "Doodles in the Margins"
	{
		_talk = REPLACE(_talk,"\b[0]","\b[%(BalloonColor)0]")
		_talk = REPLACE(_talk,"\b[2]","\b[%(BalloonColor)2]")
		_talk = REPLACE(_talk,"\b[4]","\b[%(BalloonColor)4]")
		_talk = REPLACE(_talk,"\b[6]","\b[%(BalloonColor)6]")
	}
	
	if "\![no-autopause]" !_in_ _talk
	{
		_talk = REPLACE(_talk,"... ",".\w8.\w8.\w8\w8 ")
		_talk = REPLACE(_talk,". ",".\w8\w8 ")
		_talk = REPLACE(_talk,", ",",\w4 ")
		_talk = REPLACE(_talk,": ",":\w8 ")
		_talk = REPLACE(_talk,"; ",";\w8 ")
		_talk = REPLACE(_talk,"? ","?\w8\w8 ")
		_talk = REPLACE(_talk,"! ","!\w8\w8 ")
		_talk = REPLACE(_talk,"\n\n ","\n\n\w8\w8 ")
	}
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[-1]\0\s[%(CurrentPose)]"
}

OnWindowStateRestore
{
	OnSurfaceRestore
}

On_homeurl
{
	""
}

emdash
{
	if SHIORI3FW.BalloonName != "Doodles in the Margins"; "\f[name,calibri]—\f[name,default]"
	else; "—"
}

br
{
    if SHIORI3FW.BalloonName == "Doodles in the Margins"; "\n"
    else; " "
}

OnNotifyDressupInfo
{
	//[character ID, category name, part name, option, on-1/off-0, thumbnail path] 
	Dressups.Top = IARRAY
	Dressups.Bottom = IARRAY
	Dressups.Hat = IARRAY
	Dressups.Handheld = IARRAY
	Dressups.Extra = IARRAY
	
	Dressups.Current = IARRAY
	
	foreach reference; _dressup
	{
		_dressup = SPLIT(_dressup,C_BYTE1)
		
		if _dressup[1] == "Top"; Dressups.Top ,= _dressup[2]
		if _dressup[1] == "Bottom"; Dressups.Bottom ,= _dressup[2]
		if _dressup[1] == "Hat"; Dressups.Hat ,= _dressup[2]
		if _dressup[1] == "Handheld"; Dressups.Handheld ,= _dressup[2]
		if _dressup[1] == "Extra"; Dressups.Extra ,= _dressup[2]
		
		if _dressup[4] == "1"; Dressups.Current ,= "%(_dressup[1]),%(_dressup[2])"
	}
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"These labels can be anything you want";	"https://link.example.com/link/to/whatever/here/once/more/"
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if SHIORI3FW.UserSex == "male"; "he"
	elseif SHIORI3FW.UserSex == "female"; "she"
	else; "they"
}

them
{
	if SHIORI3FW.UserSex == "male"; "him"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "them"
}

their
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "her"
	else; "their"
}

theirs
{
	if SHIORI3FW.UserSex == "male"; "his"
	elseif SHIORI3FW.UserSex == "female"; "hers"
	else; "theirs"
}

themself
{
	if SHIORI3FW.UserSex == "male"; "himself"
	elseif SHIORI3FW.UserSex == "female"; "herself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if SHIORI3FW.UserSex == "undef"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%(p('ve','s')) found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/

//This just makes %(username) match with the username determined by the SSP prefs, rather than having to use the %username environment variable which is *not* the same and which you can't do things like TOUPPER to.
//If you want to have the user be able to set a nickname or something, you may want to change username to a variable instead and get rid of this!
username
{
	SHIORI3FW.UserName
}