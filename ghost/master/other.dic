OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	if SHIORI3FW.BalloonName == "Doodles in the Margins"
	{
		//Note - we're not using input boxes so I haven't added it here...
		_talk = REPLACE(_talk,"\b[0]","\b[%(BalloonColor)0]")
		_talk = REPLACE(_talk,"\b[2]","\b[%(BalloonColor)2]")
		_talk = REPLACE(_talk,"\b[4]","\b[%(BalloonColor)4]")
		_talk = REPLACE(_talk,"\b[6]","\b[%(BalloonColor)6]")
	}
	else
	{
		_talk = REPLACE(_talk,"\b[6]","\b[6,--fallback=0]")
	}
	
	if "\![no-autopause]" !_in_ _talk
	{
		_talk = REPLACE(_talk,"... ",".\w4.\w4.\w8\w8 ")
		_talk = REPLACE(_talk,"...? ",".\w4.\w4.\w4?\w8\w8 ")
		_talk = REPLACE(_talk,"...! ",".\w4.\w4.\w4!\w8\w8 ")
		_talk = REPLACE(_talk,". ",".\w8\w8 ")
		_talk = REPLACE(_talk,", ",",\w4 ")
		_talk = REPLACE(_talk,": ",":\w8 ")
		_talk = REPLACE(_talk,"; ",";\w8 ")
		_talk = REPLACE(_talk,"? ","?\w8\w8 ")
		_talk = REPLACE(_talk,"! ","!\w8\w8 ")
		_talk = REPLACE(_talk,"%(emdash) ","%(emdash)\w8")
		_talk = REPLACE(_talk,"\n\n ","\n\n\w8\w8")
	}
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAITalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[-1]\0\s[%(CurrentPose)]\![set,alpha,100]"
}

OnWindowStateRestore
{
	OnSurfaceRestore
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/doodle_daydreams/main/"
}

emdash
{
	if SHIORI3FW.BalloonName != "Doodles in the Margins"; "\f[name,calibri]—\f[name,default]"
	else; "—"
}

br
{
    if SHIORI3FW.BalloonName == "Doodles in the Margins"; "\n"
    else; " "
}

Capitalize
{
	TOUPPER(SUBSTR(_argv[0],0,1)) + ERASE(_argv[0],0,1)
}

OnNotifyDressupInfo
{
	//[character ID, category name, part name, option, on-1/off-0, thumbnail path] 
	Dressups.Top = IARRAY
	Dressups.Bottom = IARRAY
	Dressups.Hat = IARRAY
	Dressups.Handheld = IARRAY
	Dressups.Extra = IARRAY
	
	Dressups.Current = IARRAY
	
	foreach reference; _dressup
	{
		_dressup = SPLIT(_dressup,C_BYTE1)
		
		if _dressup[1] == "Top"; Dressups.Top ,= _dressup[2]
		if _dressup[1] == "Bottom"; Dressups.Bottom ,= _dressup[2]
		if _dressup[1] == "Hat"; Dressups.Hat ,= _dressup[2]
		if _dressup[1] == "Handheld"; Dressups.Handheld ,= _dressup[2]
		if _dressup[1] == "Extra"; Dressups.Extra ,= _dressup[2]
		
		if _dressup[4] == "1"; Dressups.Current ,= "%(_dressup[1]),%(_dressup[2])"
	}
}

//Add commas for a nice display of big numbers
GroupDigits
{
	_str = TOSTR(_argv[0])
	
	_num = ""
	_decimalpoint = ""
	if "." _in_ _str
	{
		_decimalpoint = _str[1,"."]
		_num = _str[0,"."]
	}
	else; _num = _str
	
	_len = STRLEN(_num)
	
	
	_threes = _len / 3
	_excess = _len % 3
	
	_output = ""
	_i = 0
	_output += SUBSTR(_num,0,_excess)
	_i += _excess
	while _i < _len
	{
		if _output != ""; _output += ","
		_output += SUBSTR(_num,_i,3)
		_i += 3
	}
	
	if "." _in_ _str; _output += "." + _decimalpoint
	
	_output
}

On_getaistate : all
{
	"%(OUTPUTNUM('AdventurerTalk')),%(OUTPUTNUM('DemureTalk')),%(OUTPUTNUM('SleepyTalk'))"
	C_BYTE1
	"Adventurer,Demure,Sleepy"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Galla"; "https://gallathegalla.github.io/gtg-ghosts/"
	"Kipali"; "https://ako-kipali.tumblr.com/ghost-stuff"
	"Zichqec"; "https://ukagaka.zichqec.com/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Another link label";	"https://link.example.com/link/to/whatever/here/again/"
	"A third link label";	"https://link.example.com/link/to/whatever/a/third/time/you/can/add/as/many/as/you/want/"
}